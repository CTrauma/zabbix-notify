#!/usr/bin/perl
use warnings;
use strict;
use 5.010;
no if $] >= 5.018, warnings => "experimental::smartmatch";
use Data::Dumper;
use Getopt::Long;
use SlackBot;

my %contents;
my $debug  = 0;
my $nofork = 0;
my ( $api_token, $mode );

$contents{mode} = "alarm";    #default mode
$mode = "alarm";   # if not defined then default is used (can be alarm or event)
GetOptions(
    "api_token=s" => \$api_token,
    "debug"       => \$debug,
    "nofork"      => \$nofork,
    "mode=s"      => \$contents{mode}
) or die("Error in command line arguments\n");
die "You must provide 'api_token' in config file\n" unless $api_token;
die "Invalid mode is provied. Please use 'alarm' or 'event'\n"
  unless $contents{mode} =~ /alarm|event/;

#get params (from,subject,message from zabbix notification)
binmode STDOUT, ":encoding(UTF-8)";
my $channel = shift @ARGV or die "Invalid number of arguments\n";

$contents{subject} = shift @ARGV;
utf8::decode( $contents{subject} );
die "Subject provided is wrong\n"
  unless $contents{subject} =~ m/^[[:print:]]+$/;    #subject

$contents{message} = shift @ARGV;
utf8::decode( $contents{message} );
die "Message provided is wrong\n"
  unless $contents{message} =~ m/^( [[:print:]] | \t | \n | \r )+$/x;    #message

#parse contents to find something interesting (OK|PROBLEM, SEVERITY LEVEL...)
%contents = ( %contents, parse_message( $contents{message} ) );

my $slack_bot = SlackBot->new( { api_token => $api_token } );

$slack_bot->debug($debug) if $debug;
$slack_bot->channel($channel);

print Dumper $slack_bot if $debug;

binmode STDOUT, ":raw";

if ($nofork) { $slack_bot->post_message( \%contents ); }
else {
    my $pid = fork();
    if ( $pid == 0 ) {

        #child

        die "CANNOT FORK!!\n" unless defined $pid;
        open( STDOUT, '>' , "/dev/null" );    # suppressing output
        open( STDERR, '>' , "/dev/null" );    # suppressing output
        print "This is child process\n";
        $slack_bot->post_message( \%contents );
        exit 0;
    }
    print "Forked child ID is $pid\n";
    exit 0;
}
#######EXTRA SUBS################
sub parse_message {
    my $message = shift;
    my %result;
    $result{'status'} = 'PROBLEM';
    given ($message) {    #Valid values: any html code for color
        when (/eventid: *(\d+)/i) { $result{'eventid'} = $1;        continue }
        when (/\bPROBLEM\b/)      { $result{'status'}  = 'PROBLEM'; continue }
        when (/\bOK\b/) {
            $result{'status'} = 'OK';
            $result{'color'}  = '#CCFFCC';
        }
        when (/\bNot classified\b/) { $result{'color'} = '#DBDBDB' }
        when (/\bInformation\b/)    { $result{'color'} = '#CCFFCC' }
        when (/\bWarning\b/)        { $result{'color'} = '#FFFFCC' }
        when (/\bAverage\b/)        { $result{'color'} = '#FFCCCC' }
        when (/\bHigh\b/)           { $result{'color'} = '#FF9999' }
        when (/\bDisaster\b/)       { $result{'color'} = '#FF3838' }
        default                     { $result{'color'} = '#DBDBDB' }
    }
    return %result;
}

